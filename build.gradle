plugins {
    id 'java' // java 기본 설정
    id 'org.springframework.boot' version '3.0.5' // spring boot 테스크 설정
    id 'io.spring.dependency-management' version '1.1.0' // spring 의존성 관리
}

// 그룹 정보
group = 'net.harunote'

// 프로젝트 버전
version = '0.0.1-SNAPSHOT'

// 자바 컴파일 버전
sourceCompatibility = '17'

// 중앙저장소
repositories {
    mavenCentral()
}


// 하위 프로젝트에 공통적으로 적용될 설정을 지정
subprojects {
    group = 'net.harunote'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    // 관리하는 모듈의 공통 dependencies
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    // JUnit 5 테스트
    test {
        useJUnitPlatform()
    }
}


project(':rabbitmq-sample') { // 컴파일 시 multi-module-database 로딩
    dependencies {
        compileOnly project(':common-core')
    }
}

project(':api-sample') { // 컴파일 시 multi-module-database 로딩
    dependencies {
        compileOnly project(':common-core')
    }
}

project(':common-core') {
    bootJar { enabled = false }
    jar { enabled = true }
}
